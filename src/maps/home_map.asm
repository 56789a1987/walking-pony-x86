%macro switch_to_map 1
	mov dword [draw_map_func], draw_%1
	mov dword [map_handle_up_func], %1_handle_up
	call setup_%1
	call [draw_map_func]
%endmacro

setup_home_map:
	mov word [player_x], 14 * tile_size

	mov ax, [.min_x]
	mov [player_x_min], ax
	mov ax, [.max_x]
	mov [player_x_max], ax

	mov eax, [.max_y]
	mov [player_y_value], eax
	mov [player_y_max], eax

	ret

	.min_x dw 3 * tile_size + tile_size / 2
	.max_x dw 17 * tile_size - tile_size / 2
	.max_y dd 144.0

draw_home_map:
	; background
	mov al, 0
	call fill_screen

	; draw tiles
	mov esi, .tiles
	mov ecx, 8 ; 8 rows
	mov dword [draw_y], 2
	.row:
		mov dword [draw_x], 2
		push ecx
		mov ecx, 16 ; 16 columns
		.column:
			lodsb
			mov [tile_type], al
			call draw_tile
			inc dword [draw_x]
			loop .column
		pop ecx
		inc dword [draw_y]
		loop .row

	; draw objects
	draw_one_tile 3, 8, 0x15 ; bed
	draw_one_tile 4, 8, 0x16
	draw_one_tile 5, 7, 0x17 ; PC
	draw_one_tile 6, 7, 0x18
	draw_one_tile 5, 8, 0x19
	draw_one_tile 6, 8, 0x1a
	draw_one_tile 13, 7, 0x1b ; door top
	draw_one_tile 14, 7, 0x1c

	; draw name
	mov dword [draw_x], 0
	mov dword [draw_y], 0
	mov ah, 0xf
	mov esi, .name
	call draw_string

	; draw hint text
	test byte [.show_hint], 0xff
	jz .skip_hint
	mov dword [draw_x], char_width * 4
	mov dword [draw_y], char_height * 21
	mov esi, .hint
	call draw_string
	mov byte [.show_hint], 0
	.skip_hint:

	; pause menu
	call draw_menu
	ret

	.name db "Home", 0
	.hint db "Left/Right - Walk, Space - Jump", 10, "Up - Enter door, Escape - Pause", 0
	.show_hint db 1

	.tiles:
	db 0x06, 0x07, 0x08, 0x09, 0x0a, 0x09, 0x0a, 0x07, 0x0a, 0x07, 0x08, 0x09, 0x0a, 0x07, 0x08, 0x0b
	db 0x0c, 0x02, 0x03, 0x01, 0x01, 0x02, 0x03, 0x01, 0x01, 0x02, 0x03, 0x01, 0x01, 0x02, 0x03, 0x10
	db 0x0d, 0x04, 0x05, 0x01, 0x01, 0x04, 0x05, 0x01, 0x01, 0x04, 0x05, 0x01, 0x01, 0x04, 0x05, 0x11
	db 0x0e, 0x01, 0x01, 0x02, 0x03, 0x01, 0x01, 0x02, 0x03, 0x01, 0x01, 0x02, 0x03, 0x01, 0x01, 0x10
	db 0x0d, 0x01, 0x01, 0x04, 0x05, 0x01, 0x01, 0x04, 0x05, 0x01, 0x01, 0x04, 0x05, 0x01, 0x01, 0x12
	db 0x0c, 0x02, 0x03, 0x01, 0x01, 0x02, 0x03, 0x01, 0x01, 0x02, 0x03, 0x01, 0x01, 0x02, 0x03, 0x10
	db 0x0d, 0x04, 0x05, 0x01, 0x01, 0x04, 0x05, 0x01, 0x01, 0x04, 0x05, 0x1d, 0x1e, 0x04, 0x05, 0x11
	db 0x0f, 0x14, 0x14, 0x14, 0x14, 0x14, 0x14, 0x14, 0x14, 0x14, 0x14, 0x14, 0x14, 0x14, 0x14, 0x13

home_map_handle_up:
	; door left <= x < door right
	movsx eax, word [player_x]
	cmp eax, 13 * tile_size
	jl .end
	cmp eax, 15 * tile_size
	jnl .end

	mov eax, [player_y]
	cmp eax, 8 * tile_size
	jl .end

	switch_to_map outside_map

	.end:
	ret
